package eu.ddmore.converter.mdl2pharmml08

import eu.ddmore.mdl.mdl.MclObject
import eu.ddmore.mdl.utils.MdlUtils
import org.eclipse.emf.ecore.util.EcoreUtil
import org.eclipse.xtext.EcoreUtil2

import static eu.ddmore.converter.mdl2pharmml08.Constants.*
import eu.ddmore.converter.mdl2pharmml.MdlRootProvider
import eu.ddmore.mdl.mdl.Mcl
import eu.ddmore.converter.mdl2pharmml.VectorAttributeRewrite

class Mdl2Pharmml {
	static val mdlVersion = "6.0"
	
	extension MdlUtils mu = new MdlUtils
	extension MdlRootProvider mrp = new MdlRootProvider
	extension ModelDefinitionPrinter mdp = new ModelDefinitionPrinter
	extension ModellingStepsPrinter msp = new ModellingStepsPrinter
	extension FunctionDefinitionPrinter fdp = new FunctionDefinitionPrinter

	var Mcl mdlRoot

	private def copyMdl(Mcl orig){
		mdlRoot = EcoreUtil.copy(orig)
//		mdlRoot = orig
	}

	private def rewriteTree(){
		val vectArgR = new VectorAttributeRewrite
		var cntr = 0
		val iter = mdlRoot.eAllContents
		while(iter.hasNext){
			val node = iter.next
			if(vectArgR.doSwitch(node) != null) cntr++	
		}
//		val finalCount = cntr
	}
	

  	def convertToPharmML(MclObject mog) {
  		copyMdl(EcoreUtil2.getContainerOfType(mog.eContainer, Mcl))
  		rewriteTree()
		
		'''
		<?xml version="1.0" encoding="UTF-8"?>
		<PharmML 
			«printPharmMlNameSpaces»
			writtenVersion="«writtenVersion»">
			<ct:Name>"generated by MDL2PharmML v.«mdlVersion»"</ct:Name>
			<IndependentVariable symbId="«mdlRoot.mdlObj.mdlIdv?.name ?: "T"»"/>
			«mdlRoot.mdlObj.writeFunctionDefinitions»	
			«mdlRoot.mdlObj.writeModelDefinition(mdlRoot.paramObj)»
			«writeModellingSteps(mdlRoot)»
		</PharmML>
		'''			
	}

	//+ Print PharmML namespaces
	protected def printPharmMlNameSpaces()
		'''
		xmlns:xsi="«xsi»" 
		xmlns="«xmlns_pharmML»"
		xsi:schemaLocation="«xsi_schemaLocation»"
		xmlns:math="«xmlns_math»"
		xmlns:ct="«xmlns_ct»"
		xmlns:ds="«xmlns_ds»"
		xmlns:mdef="«xmlns_mdef»"
		xmlns:mstep="«xmlns_mstep»"
		xmlns:design="«xmlns_design»"
		'''
}