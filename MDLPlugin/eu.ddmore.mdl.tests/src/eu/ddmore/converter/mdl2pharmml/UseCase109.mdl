# Version: 1.3.0 (Prod4.1 alpha release) Build Number: 1422 Build ID: 2015-11-19_11-27-23

### Data object
Friberg_PANSS_CPT2009_dat = dataObj{

   DECLARED_VARIABLES {Y}
   
   DATA_INPUT_VARIABLES{
      ID : {use is id}
      TIME : {use is idv} # days
      DV : {use is dv, variable =Y}
      STUD : { use is covariate}  # Study number, 1st digit indicates phase'
      DDUR : {use is covariate}  # Duration of current episode
      HOSP : {use is covariate}  # Patient Hospitalised at time of observation (no=0, yes=1)
      US : {use is covariate} # Study performed in US (no=0, yes=1)
      AUC : {use is covariate} # ug/L*h"
      MDV : {use is mdv}
   }# end DATA_INPUT_VARIABLES


   SOURCE{
      scrfile : {
           file="PANSS_Friberg2009_simdata_2.csv",
           inputFormat is nonmemFormat}
   }# end SOURCE
} # end data object


### Parameter object
Friberg_PANSS_CPT2009_par = parObj{

   DECLARED_VARIABLES {ETA_PAN0 ETA_PMAX}

   STRUCTURAL{
      PAN0_II : {value=94}
      PAN0_III : {value=90.5}
      PAN0_CHRON : {value=-0.0339}
      TVPMAX : {value=0.0859}
      PMAX_PHASEIII : {value=0.688} 
      TD : {value=13.2,lo=0} 
      POW : {value=1.24,lo=0}
      POP_AUC50 : {value=82} 
      EMAX : {value=0.191}
      THETA_HOSP: {value=-0.145}
      THETA_US: {value=0.623}
      TVERROR : {value=3.52}
   }# end STRUCTURAL

   VARIABILITY{
      IIV_PAN0 : {value=167, type is var}
      IIV_PMAX : {value=0.0249, type is var}
      
      CORR_PAN0_PMAX : {parameter=[ETA_PAN0,ETA_PMAX], value=[-0.395], type is cov}
      
      IIV_AUC50 : { value= 21.7, type is var}
      IIV_ERROR : {value=0.196, type is var}
      
      SIGMA : {value=1, type is var,  fix=true}
   
   }# end VARIABILITY

} # end of parameter object

### Model object
Friberg_PANSS_CPT2009_mdl = mdlObj{

  IDV {TIME}
  
  COVARIATES {
   DDUR
   STUD
   HOSP
   US
   AUC
   
   DDU = if (DDUR > 2) then 1  # duration > 1 month
         else 0 # duration of current episode < 1 month
 
   PHASE = if (STUD > 30) then 1  # phase III study
  	       else 0   # phase II
  }

   STRUCTURAL_PARAMETERS{
      PAN0_II        # baseline PANSS phaseII studies
      PAN0_III       # baseline PANSS phaseIII studies
      PAN0_CHRON     # proportional difference in PAN0 for chronic patients
      TVPMAX         # Maximum placebo effect phase_II
      PMAX_PHASEIII  # Proportional difference in PMAX for patients in Phase III
      TD             # Time to achieve PMAX
      POW            # Power parameter in the Weibull function
      POP_AUC50      # Asenapine AUC to achieve half of EMAX
      EMAX           # Maximum asenapine effect at day42
      THETA_HOSP     # Difference in residual error for hospitalized patients
      THETA_US       # Difference in residual error in US-based studies
      TVERROR      # Typical_residual_error
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      IIV_PAN0
      IIV_PMAX
      IIV_AUC50
      IIV_ERROR
      
      SIGMA
   }# end VARIABILITY_PARAMETERS

   GROUP_VARIABLES{		
   #baseline PANSS		
    POP_PAN0 = if (PHASE == 0) then PAN0_II*(1+PAN0_CHRON*DDU)  # for phase II
               elseif (PHASE==1) then PAN0_III*(1+PAN0_CHRON*DDU);  # for phase I
   
   #maximum placebo response
   POP_PMAX = TVPMAX*(1+PMAX_PHASEIII*PHASE)
   
   # Difference in the residual error for non-hospitalised patients
   CHOSP = if (HOSP==0) then 1 + THETA_HOSP
            else 1 
   
   #Error 
   POP_ERROR = CHOSP*(TVERROR*(1 + THETA_US*US))
   }# end GROUP_VARIABLES

   VARIABILITY_LEVELS{
    ID : {type is parameter, level=2}
    DV : {type is observation, level=1}
   }

   RANDOM_VARIABLE_DEFINITION(level=ID){
      ETA_PAN0 ~ Normal(mean=0, var=IIV_PAN0)
      ETA_PMAX ~ Normal(mean=0, var=IIV_PMAX)
      ETA_AUC50 ~ Normal(mean=0, var=IIV_AUC50)
      ETA_ERROR ~ Normal(mean=0, var=IIV_ERROR)
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
    PAN0 = POP_PAN0 + ETA_PAN0
    PMAX = POP_PMAX + ETA_PMAX
    AUC50 = POP_AUC50*exp(ETA_AUC50)  
    ERROR = POP_ERROR*exp(ETA_ERROR)
   }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
    # placebo model
    PMOD = PMAX*(1-exp(-(TIME/TD)^ POW))  
   
    # time-course parameter for asenapine response
    FT = if (TIME>42) then 1
         else TIME/42 
   
    # Asenapine effect
    EFF = EMAX*AUC/(AUC50+AUC) * FT # Drug effect only until day 42 
        
    EMOD = 	if (TIME>0 && AUC>0) then EFF  # Define drug effect at times greater than 0
    	    else 0
    
    #Total PANSS score 
     PANSS_total = PAN0*(1-PMOD)*(1-EMOD)
   }# end MODEL_PREDICTION

  RANDOM_VARIABLE_DEFINITION(level=DV){  
      EPS_SIGMA ~ Normal(mean=0, var=SIGMA) 
      }
 
   OBSERVATION{
      Y = additiveError(additive = ERROR, prediction= PANSS_total, eps=EPS_SIGMA)
   }# end OBSERVATION


} # end of model object

Friberg_PANSS_CPT2009_task = taskObj{
### Task Properties object
      ESTIMATE {
         set algo is focei
      }# end ESTIMATE

}# end of task object


warfarin_PK_ODE_mog = mogObj {
	OBJECTS{
		Friberg_PANSS_CPT2009_dat : { type is dataObj }
		Friberg_PANSS_CPT2009_mdl : { type is mdlObj }
		Friberg_PANSS_CPT2009_par : { type is parObj }
		Friberg_PANSS_CPT2009_task : { type is taskObj }
	}
}
