# MAGNI 2000

magni2000_data = dataObj{

DECLARED_VARIABLES{Y1 Y2 Y3 Y4}

   DATA_INPUT_VARIABLES{
      ID:{use is id}
      TIME:{use is idv} #ALWAYS ZERO
      WEIGHT : { use is covariate }
      HEIGHT : { use is covariate }
      #HSTATUS : { use is catCov withCategories {normal when 1, obese when 2, diabetic when 3} } 
      HSTATUS : { use is covariate }
      AGE : { use is covariate }
      #FEMALE : { use is catCov withCategories {yes when 1, no when 0}}
      FEMALE : { use is covariate}
      DV : { use is dv, define={1 in DVID as Y1, 2 in DVID as Y2, 3 in DVID as Y3, 4 in DVID as Y4} }
      DVID : { use is dvid }
   }
   
   SOURCE{
       srcfile: {file = "magni2000_subjects.csv", 
            	  inputFormat is nonmemFormat
            	}
   }
}

magni2000_par = parObj{

DECLARED_VARIABLES{eta_ts eta_F eta_tl eta_V}

   STRUCTURAL{
   
	mtsn : {value=4.99}
	mtso : {value=4.50}
	mtsd : {value=4.70}
	mFn : {value=0.767}
	mFo : {value=0.781}
	mFd : {value=0.778}
	atl : {value=26.70}
	btl : {value=0.209}
	aVm : {value=0.417}
	bVm : {value=0.89}
	aVf : {value=2.04}
	bVf : {value=1.77}
	ADD_SMALL : {value=1E-20}
   	}
   
   VARIABILITY{
	SIGMA2_ts : {value=1.3, type is var}
	SIGMA2_F : {value=0.0022, type is var}
	SIGMA2_tl : {value=32.94, type is var}
	SIGMA2_V : {value=0.71, type is var}
	
	MATRIX_1: {
		type is cov,
		parameter=[eta_ts, eta_F, eta_tl, eta_V],
		value=[0.0062, 3.24, 0.071, 0.59, -0.0055, 1.89]
      }
	}

}

magni2000_mdl = mdlObj{
 	
 	IDV {T}
 	
 	COVARIATES{
		#HSTATUS withCategories {normal, obese, diabetic}
		#FEMALE withCategories {yes, no}
		HSTATUS
		FEMALE
		AGE
		WEIGHT
		HEIGHT
		BSA = (WEIGHT^0.425)*(HEIGHT^0.725)*0.007184
	}
 	
 	VARIABILITY_LEVELS{
   	DV : { level=1, type is observation }
   	ID : { level=2, type is parameter }
   }

	
	STRUCTURAL_PARAMETERS{
	mtsn
	mtso
	mtsd
	mFn
	mFo
	mFd
	atl
	btl
	aVm
	bVm
	aVf
	bVf
	ADD_SMALL
	}
	
	VARIABILITY_PARAMETERS{
	SIGMA2_ts
	SIGMA2_F
	SIGMA2_tl
	SIGMA2_V
	}
		
   	
   	GROUP_VARIABLES {
   	
	   	#BSA = (WEIGHT^0.425)*(HEIGHT^0.725)*0.007184
   	
   		#GROUP_ts = if (HSTATUS==HSTATUS.normal) then mtsn
   		#	 	   else if (HSTATUS==HSTATUS.obese) then mtso
   		#	 	   else mtsd;
   			 
   		#GROUP_F = if (HSTATUS==HSTATUS.normal) then mFn
   		#		  else if (HSTATUS==HSTATUS.obese) then mFo
   		#		  else mFd;
   			
   		GROUP_tl = atl+btl*AGE
   		
   		#GROUP_V = if(FEMALE==FEMALE.no) then aVm+bVm*BSA
   		#		  else aVf+bVf*BSA
   				  
 		GROUP_ts = if (HSTATUS==1) then mtsn
   			 	   if (HSTATUS==2) then mtso
   			 	   else mtsd
   			 
   		GROUP_F = if (HSTATUS==1) then mFn
   				  if (HSTATUS==2) then mFo
   				  else mFd
   			
   		GROUP_V = if(FEMALE==0) then aVm+bVm*BSA
   				  else aVf+bVf*BSA
   	}
   	
   	INDIVIDUAL_VARIABLES{
    	ts_IND = linear(pop=GROUP_ts, ranEff=[eta_ts])
		F_IND = linear(pop=GROUP_F, ranEff=[eta_F])
		tl_IND = linear(pop=GROUP_tl, ranEff=[eta_tl])
		V_IND = linear(pop=GROUP_V, ranEff=[eta_V])
   	}
   	
   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_ts ~ Normal(mean=0, var=SIGMA2_ts)
      eta_F ~ Normal(mean=0, var=SIGMA2_F)
      eta_tl ~ Normal(mean=0, var=SIGMA2_tl)
      eta_V ~ Normal(mean=0, var=SIGMA2_V)
   }# end RANDOM_VARIABLE_DEFINITION
	
   RANDOM_VARIABLE_DEFINITION(level=DV){
	  EPS  ~ Normal(mean=0, var=1 )
   }# end RANDOM_VARIABLE_DEFINITION
   
  
	MODEL_PREDICTION {
		ts_PRED = ts_IND
		F_PRED = F_IND
		tl_PRED = tl_IND
		V_PRED = V_IND
	}	
   
   
   OBSERVATION {	
 		Y1 = additiveError(additive=ADD_SMALL, eps=EPS, prediction= ts_PRED)
 		Y2 = additiveError(additive=ADD_SMALL, eps=EPS, prediction= F_PRED)
 		Y3 = additiveError(additive=ADD_SMALL, eps=EPS, prediction= tl_PRED)
 		Y4 = additiveError(additive=ADD_SMALL, eps=EPS, prediction= V_PRED)
 	}
}
   
magni2000_task = taskObj{ 
  ESTIMATE { 
    	set algo is foce
	}
}
	
magni2000_mog =mogObj {
	OBJECTS{
	magni2000_data : { type is dataObj }
	magni2000_mdl : { type is mdlObj }
	magni2000_par : { type is parObj }
	magni2000_task : { type is taskObj }
	}
	
}
