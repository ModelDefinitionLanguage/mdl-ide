# Warfarin model with oral and IV bolus dosing
#
# Model as an ODE and covariate already transformed.
#

warfarin_PK_IVPO_dat = dataObj {
	DECLARED_VARIABLES{
		GUT
		CENTRAL
		Y
	}

  DATA_INPUT_VARIABLES{
      ID: 	{ use is id }
      TIME: { use is idv }
      WT: 	{ use is covariate }
      AMT: 	{ use is amt, define={1 in CMT as GUT, 2 in CMT as CENTRAL} }
      RATE: { use is rate }
      CMT: 	{ use is cmt }
      DV: 	{ use is dv, variable=Y}
      logtWT: { use is covariate }
   }# end DATA_INPUT_VARIABLES

   SOURCE{
      srcfile : { file="warfarin_infusion_oral.csv",
      			  inputFormat is nonmemFormat}
   }# end SOURCE
} # end data object

warfarin_PK_IVPO_par = parObj {
	DECLARED_VARIABLES{ETA_CL ETA_V}

	# By default a parameter is to be estimated if fix is omitted
 	STRUCTURAL {
		POP_CL : { value = 0.1, lo = 0.001 }
		POP_V : { value = 8, lo = 0.001 } 
		POP_KA : { value = 0.362, lo = 0.001 }
		POP_TLAG : { value=1, lo=0.001, hi=10 }
		POP_FORAL : { value=0.7, lo=0.001 }
		BETA_CL_WT : { value = 0.75, fix = true }
		BETA_V_WT : { value = 1, fix = true } 
		RUV_PROP : { value = 0.1, lo = 0 }
		RUV_ADD : { value = 0.1, lo = 0} 
		} # end STRUCTURAL
	VARIABILITY {
		PPV_CL : { value = 0.1, type is sd }
		PPV_V : { value = 0.1, type is sd }
		PPV_KA : { value = 0.1, type is sd }
		PPV_TLAG : { value = 0.1, type is sd } 
		PPV_FORAL : { value = 0.1, type is sd }
		OMEGA : { parameter=[ETA_CL, ETA_V], value = [0.01], type is corr }
	} # end VARIABILITY
} # end of parameter object 

warfarin_PK_IVPO_mdl = mdlObj {
	IDV{ T }

	COVARIATES{
		logtWT
	}

	VARIABILITY_LEVELS{
		ID : { level=2, type is parameter }
		DV : { level=1, type is observation }
	} 

	STRUCTURAL_PARAMETERS { 
		POP_CL
		POP_V
		POP_KA
		POP_TLAG
		POP_FORAL
		BETA_CL_WT
		BETA_V_WT
		RUV_PROP
		RUV_ADD
	} # end STRUCTURAL_PARAMETERS
	
	VARIABILITY_PARAMETERS {
		PPV_CL
		PPV_V
		PPV_KA
		PPV_TLAG
		PPV_FORAL
	} # end VARIABILITY_PARAMETERS 
	
	RANDOM_VARIABLE_DEFINITION(level=ID) {
		ETA_CL ~ Normal(mean = 0, sd = PPV_CL)
		ETA_V ~ Normal(mean = 0, sd = PPV_V)
		ETA_KA ~ Normal(mean = 0, sd = PPV_KA)
		ETA_TLAG ~ Normal(mean = 0, sd = PPV_TLAG) # We define correlation here
		ETA_FORAL ~ Normal(mean = 0, sd = PPV_FORAL)
	} # end RANDOM_VARIABLE_DEFINITION 

	
	INDIVIDUAL_VARIABLES { # This maps to the "Type 3" individual parameter definition in PharmML
	    ln(CL) = linear( trans is ln, pop = POP_CL, fixEff = [{coeff=BETA_CL_WT, cov=logtWT}] , ranEff = [ETA_CL] )
	    ln(V) = linear( trans is ln, pop = POP_V, fixEff = [{coeff=BETA_V_WT, cov=logtWT}] , ranEff = [ETA_V] )
	    ln(KA) = linear( trans is ln, pop = POP_KA, ranEff = [ETA_KA] )
	    ln(TLAG) = linear( trans is ln, pop = POP_TLAG, ranEff = [ETA_TLAG] )
	    ln(FORAL) = linear( trans is ln, pop = POP_FORAL, ranEff = [ETA_FORAL] )
	} # end INDIVIDUAL_VARIABLES

	MODEL_PREDICTION {
		DEQ{
			RATEIN = if(T >= TLAG) then GUT * KA * FORAL 
					 else 0
			GUT : { deriv =(- RATEIN), init = 0, x0 = 0 }
			CENTRAL : { deriv =(RATEIN - CL * CENTRAL / V), init = 0, x0 = 0 } 
		}
	    CC = CENTRAL / V 
	} # end MODEL_PREDICTION
	
	RANDOM_VARIABLE_DEFINITION(level=DV){
	    EPS_Y ~ Normal(mean = 0, var = 1) # This maps the standard error model in PharmML. The error model is predefined.
	}

	OBSERVATION {
	    Y = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, eps = EPS_Y, prediction = CC ) 
	} # end OBSERVATION
} # end of model object

warfarin_PK_ODE_task = taskObj {
	ESTIMATE{ 
		set algo is saem 
}
} # end of task object
warfarin_PK_ODE_mog = mogObj {
    OBJECTS{
		warfarin_PK_IVPO_dat : { type is dataObj }
		warfarin_PK_IVPO_mdl : { type is mdlObj }
		warfarin_PK_IVPO_par : { type is parObj }
		warfarin_PK_ODE_task : { type is taskObj }
	}
}





