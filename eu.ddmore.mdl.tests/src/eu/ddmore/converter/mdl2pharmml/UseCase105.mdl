drugX_delAbs_1occ_v1_dat = dataObj{

   DECLARED_VARIABLES{Cc_obs}

   DATA_INPUT_VARIABLES{
      ID: {use is id}
      DV: {use is dv, variable=Cc_obs}
      TIME: {use is idv}
      MDV: {use is mdv}
      AMT: {use is covariate}
      RATE: {use is covariate}
      WT: {use is covariate}
      CMT: {use is cmt}
   }# end DATA_INPUT_VARIABLES

   SOURCE{
      srcfile: {file="drugX_PO_1occ_dataMDL_v1.csv",# simulated data
      inputFormat is nonmemFormat}
   }# end SOURCE
   
} # end data object

drugX_delAbs_1occ_v1_par = parObj{

   STRUCTURAL{
      pop_Vc: {value=10, lo=0, hi=inf}
      pop_Vp: {value=30, lo=0, hi=inf}
      pop_CL: {value=6, lo=0, hi=inf}
      pop_Ka: {value=1.5, lo=0, hi=inf}
      pop_Q: {value=10, lo=0, hi=inf}
	  pop_MTT: {value=1, lo=0}
	  pop_n: {value=9, lo=0}      
      b: {value=.15, lo=0, hi=inf}
   }# end STRUCTURAL

   VARIABILITY{
      omega_Vc: {value=.16, type is var}
      omega_Vp: {value=.25, type is var}
      omega_CL: {value=.49, type is var}
      sigma: {value=1, type is var, fix=true}
   }# end VARIABILITY

} # end of parameter object

drugX_delAbs_1occ_v1_mdl = mdlObj{

   IDV{T}
   COVARIATES{
      WT
      AMT
      RATE
   }# end COVARIATES

   VARIABILITY_LEVELS{
      ID: {type is parameter, level=2}
      DV: {type is observation, level=1}
   }# end VARIABILITY_LEVELS

   STRUCTURAL_PARAMETERS{
      pop_Vc
      pop_Vp
      pop_CL
      pop_Ka
      pop_Q
      pop_MTT
      pop_n
      b
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      omega_Vc
      omega_Vp
      omega_CL
      sigma
   }# end VARIABILITY_PARAMETERS

   GROUP_VARIABLES{
      grp_Vc = pop_Vc*(WT/70)
   }# end GROUP_VARIABLES

   RANDOM_VARIABLE_DEFINITION (level=ID) {
      eta_Vc ~ Normal(mean=0, var=omega_Vc)
      eta_Vp ~ Normal(mean=0, var=omega_Vp)
      eta_CL ~ Normal(mean=0, var=omega_CL)

   }# end RANDOM_VARIABLE_DEFINITION (level=ID)

   RANDOM_VARIABLE_DEFINITION (level=DV) {
      epsilon_Cc ~ Normal(mean=0, var=sigma)
   }# end RANDOM_VARIABLE_DEFINITION (level=DV)

   INDIVIDUAL_VARIABLES{
      Vc = grp_Vc*exp(eta_Vc)
      Vp = pop_Vp*exp(eta_Vp)
      CL = pop_CL*exp(eta_CL)
      ka = pop_Ka
      Q = pop_Q
      MTT = pop_MTT
      n = pop_n
      kcp = Q/Vc
      kpc = Q/Vp
      kel = CL/Vc
      ktr = (n+1)/MTT
      Dose = if (AMT>0) then AMT else 0# to be checked with the dataset!!!!!!!!!!!!!!!!!!!!!!!!!!
      
      amin = min(Dose, n) # for testing purposes
      amax = max(Dose, n) # for testing purposes
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{

      DEQ{    
         # to be checked with the dataset:!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
         Aader = if (T==0) then 0
            else exp(ln(Dose) + ln(ktr) + n*ln(ktr*T) - ktr*T - lnFactorial(n)) - ka*Aa
          
         Aa: {deriv= Aader, init= 0}
         Ac: {deriv= ka*Aa-kel*Ac-kcp*Ac+kpc*Ap, init= 0}
         Ap: {deriv= kcp*Ac-kpc*Ap, init= 0}
      }# end DEQ

      Cc = Ac/Vc
   }# end MODEL_PREDICTION

   OBSERVATION{
      Cc_obs =  Cc+b*epsilon_Cc
   }# end OBSERVATION


} # end of model object

drugX_delAbs_1occ_v1_task = taskObj {
	ESTIMATE{ 
		set algo is foce# should be focei!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	}
} # end of task object

drugX_delAbs_1occ_v1_mog=mogObj{
	OBJECTS{
		drugX_delAbs_1occ_v1_dat: {type is dataObj}
		drugX_delAbs_1occ_v1_par: {type is parObj}
		drugX_delAbs_1occ_v1_mdl: {type is mdlObj}
		drugX_delAbs_1occ_v1_task: {type is taskObj}
	}
}
