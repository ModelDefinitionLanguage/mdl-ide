
warfarin_PK_ODE_dat = dataObj {
	DECLARED_VARIABLES{GUT Y}

	DATA_INPUT_VARIABLES {
		ID : { use is id }
		TIME : { use is idv }
		WT : { use is covariate }
		AMT : { use is amt, define = {1 in CMT as GUT} }
		CMT : { use is cmt }
		DVID : {  use is dvid}
		DV : { use is dv, variable = Y }
		MDV : {  use is mdv }
		logtWT:{ use is covariate }  
	} # end DATA_INPUT_VARIABLES
	
	SOURCE {
	   srcFile : { file = "warfarin_conc_cmt.csv", 
            		inputFormat is nonmemFormat} 
	} # end SOURCE
} # end data object

warfarin_PK_Latent_par = parObj{
	DECLARED_VARIABLES{ETA_CL ETA_V}

	# By default a parameter is to be estimated if fix is omitted
 	STRUCTURAL {
		POP_CL : { value = 0.1, lo = 0.001 }
		POP_V : { value = 8, lo = 0.001 } 
		POP_KA : { value = 0.362, lo = 0.001 }
		POP_TLAG : { value=1, lo=0.001, hi=10 }
		BETA_CL_WT : { value = 0.75, fix = true }
		BETA_V_WT : { value = 1, fix = true } 
        POP_KT: {value=1, lo=0.001}
        POP_K1: {value=0.1, lo=0.001}
        POP_P1: {value=1 ,fix=true}
		RUV_PROP : { value = 0.1, lo = 0 }
		RUV_ADD : { value = 0.1, lo = 0 } 
		} # end STRUCTURAL
	VARIABILITY {
		PPV_CL : { value = 0.1, type is sd }
		PPV_V : { value = 0.1, type is sd }
		PPV_KA : { value = 0.1, type is sd }
		PPV_TLAG : { value = 0.1, type is sd } 
		OMEGA : { parameter=[ETA_CL, ETA_V], value = [0.01], type is corr }
	} # end VARIABILITY
} # end of parameter object 

warfarin_PK_Latent_mdl = mdlObj{

   IDV{ T }
	COVARIATES{
		logtWT
	}

	VARIABILITY_LEVELS{
		ID : { level=2, type is parameter }
		DV : { level=1, type is observation }
	} 

   STRUCTURAL_PARAMETERS{
      POP_CL
      POP_V
      POP_KA
      POP_TLAG
      BETA_CL_WT
      BETA_V_WT
      POP_KT
      POP_K1
      POP_P1
      RUV_PROP
      RUV_ADD
   }# end STRUCTURAL_PARAMETERS

	VARIABILITY_PARAMETERS {
		PPV_CL
		PPV_V
		PPV_KA
		PPV_TLAG
	} # end VARIABILITY_PARAMETERS 
	
	RANDOM_VARIABLE_DEFINITION(level=ID) {
		ETA_CL ~ Normal(mean = 0, sd = PPV_CL)
		ETA_V ~ Normal(mean = 0, sd = PPV_V)
		ETA_KA ~ Normal(mean = 0, sd = PPV_KA)
		ETA_TLAG ~ Normal(mean = 0, sd = PPV_TLAG) # We define correlation here
	} # end RANDOM_VARIABLE_DEFINITION 

   INDIVIDUAL_VARIABLES{
	  ln(CL) = linear( trans is ln, pop = POP_CL, fixEff = [{coeff=BETA_CL_WT, cov=logtWT}] , ranEff = [ETA_CL] )
	  ln(V) = linear( trans is ln, pop = POP_V, fixEff = [{coeff=BETA_V_WT, cov=logtWT}] , ranEff = [ETA_V] )
	  ln(KA) = linear( trans is ln, pop = POP_KA, ranEff = [ETA_KA] )
	  ln(TLAG) = linear( trans is ln, pop = POP_TLAG, ranEff = [ETA_TLAG] )
	  ALAG1=TLAG
      F1=POP_P1
      S3=V
      K12=KA # absorption from GUT to LATENT
      K20=POP_K1 # elimination from LATENT
      K23=POP_KT # transfer from LATENT to CENTRAL
      K30=CL/V # elimination from CENTRAL

   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{

   COMPARTMENT{
      ##  Parameters = K12,K20,K23,K30,F1,ALAG1
      GUT:     {type is depot, to=LATENT, ka=K12, tlag=ALAG1, finput=F1}
      LATENT:  {type is compartment }
            :: {type is elimination, from=LATENT, k=K20}
            :: {type is transfer, from=LATENT, to=CENTRAL, kt=K23}
      CENTRAL: {type is compartment }
            :: {type is elimination, from=CENTRAL, k=K30}
   }# end COMPARTMENT

      F=CENTRAL/S3

#    LIBRARY
#       F=PK(glm=true, ncomp=3)
#    }# end LIBRARY
# 
      CC=F
   
   }# end MODEL_PREDICTION

	RANDOM_VARIABLE_DEFINITION(level=DV){
	    EPS_Y ~ Normal(mean = 0, var = 1) # This maps the standard error model in PharmML. The error model is predefined.
	}

	OBSERVATION {
	    Y = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, eps = EPS_Y, prediction = CC) 
	} # end OBSERVATION

} # end of model object

warfarin_PK_ODE_task = taskObj {
	ESTIMATE{ 
		set algo is saem 
	}
} # end of task object

#Modelling object group

warfarin_PK_Latent__mog=mogObj{
	OBJECTS{
		warfarin_PK_ODE_dat : { type is dataObj }
		warfarin_PK_Latent_par : { type is parObj }
		warfarin_PK_Latent_mdl : { type is mdlObj }
		warfarin_PK_ODE_task : { type is taskObj }
	}
}
